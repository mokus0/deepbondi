#!/usr/bin/perl

use strict;

# XAuth "MIT-MAGIC-COOKIE" host-to-cookie map
my %xauth;
# Set of cookies in %xauth
my %cookies;

# initialize %xauth and %cookies based on "xauth list" output:
open XAUTH, "-|", "/usr/X11/bin/xauth", "list" or die $!;

while (<XAUTH>) {
  if (/^([^ \t]+)\s+MIT-MAGIC-COOKIE-1\s+([0-9a-fA-F]+)$/) {
    $xauth{lc $1} = $2;
    $cookies{$2}++;
  }
}

close XAUTH;

# Look for a cookie matching $DISPLAY or `hostname`:0
my $cookie = $xauth{lc $ENV{"DISPLAY"}};

if (!defined($cookie)) {
    my $hostname = `/bin/hostname`;
    chomp $hostname;
    $hostname = lc $hostname;
    
    $cookie = $xauth{$hostname . ":0"};
}

# If one wasn't found, but there's only one cookie that came up, return that one:
if (!defined($cookie) && scalar(keys %cookies) == 1) {
  $cookie = (keys %cookies)[0];
}

# print the one that was found, if any:
exit(1) if !defined($cookie);
print "$cookie\n";
